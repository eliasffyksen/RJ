WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

func = {
  "fn" ~ ident ~ arg_def
  ~ (":" ~ ret_type)?
  ~ block
}

ret_type = {
  var_type ~ ("," ~ var_type)*
}

ident = @{
  ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")*
}

arg_def= {
  "("
  ~ (var_decl ~ ("," ~ var_decl)*)?
  ~ ")"
}

block = {
  "{" ~ stmt* ~ "}"
}

stmt = {
  var_decl
  | func_ret
  | assign
  | func_call
  | if_stmt
}

if_stmt = {
  "if" ~ expr_elm ~ block
  ~ else_stmt?
}

else_stmt = {
  "else" ~ block
}

func_ret = {
  "return" ~ expr_list?
}

assign = {
  ident ~ ("," ~ ident)* ~ "=" ~ expr_list
}

expr_list = {
  expr_elm ~ ("," ~ expr_elm)*
}

expr_elm = {
 pred_0
}

pred_0 = {
  cmp
  | pred_1
}

pred_1 = {
  sum
  | pred_2
}

pred_2 = {
  pred_max
}

pred_max = {
  func_call
  | int
  | bool
  | ident
}

cmp = {
  pred_1 ~ (
    cmp_eq
    | cmp_ne
    | cmp_le
    | cmp_ge
    | cmp_lt
    | cmp_gt
  ) ~ pred_1
}

cmp_eq = { "==" }
cmp_ne = { "!=" }
cmp_lt = { "<" }
cmp_le = { "<=" }
cmp_gt = { ">" }
cmp_ge = { ">=" }

sum = {
  pred_2 ~ (( add | sub ) ~ pred_2)+
}

add = { "+" }
sub = { "-" }

int = @{
  "-"? ~ '0'..'9'+
}

bool = { "true" | "false" }

func_call = {
  ident ~ "(" ~ expr_list? ~ ")"
}

var_decl = {
  ident ~ ":" ~ var_type
}

var_type = {
  "i32"
  | "bool"
}

file = {
  SOI ~ func* ~ EOI
}
