WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

func = {
  "fn" ~ ident ~ arg_def
  ~ (":" ~ ret_type)?
  ~ block
}

ret_type = {
  var_type ~ ("," ~ var_type)*
}

ident = @{
  ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")*
}

arg_def= {
  "("
  ~ (var_decl ~ ("," ~ var_decl)*)?
  ~ ")"
}

block = {
  "{" ~ stmt* ~ "}"
}

stmt = {
  var_decl
  | func_ret
  | assign
  | func_call
  | if_stmt
}

if_stmt = {
  "if" ~ expr_elm ~ block
}

func_ret = {
  "return" ~ expr_list?
}

assign = {
  ident ~ ("," ~ ident)* ~ "=" ~ expr_list
}

expr_list = {
  expr_elm ~ ("," ~ expr_elm)*
}

expr_elm = {
 pred_0
}

pred_0 = {
  cmp
  | pred_max
}

pred_max = {
  func_call
  | int
  | ident
}

cmp = {
  pred_max ~ (
    cmp_eq
    | cmp_ne
    | cmp_lt
    | cmp_gt
  ) ~ pred_max
}

cmp_eq = { "==" }
cmp_ne = { "!=" }
cmp_lt = { "<" }
cmp_gt = { ">" }

int = @{
  "-"? ~ '0'..'9'+
}

func_call = {
  ident ~ "(" ~ expr_list? ~ ")"
}

var_decl = {
  ident ~ ":" ~ var_type
}

var_type = {
  "i32"
}

file = {
  SOI ~ func* ~ EOI
}
